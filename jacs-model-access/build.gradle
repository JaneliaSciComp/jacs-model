project(":jacs-model-access") {

    configurations {
        integrationTestCompileOnly.extendsFrom(testCompileOnly)
        integrationTestImplementation.extendsFrom(testImplementation)
        integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationTest/java')
            }
            resources {
                srcDir file('src/integrationTest/resources')
            }
        }
    }

    compileIntegrationTestJava {
        doFirst {
            options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
            ]
        }
    }

    dependencies {
        api project(":jacs-model-core")

        implementation JACKSON_DATABIND_LIB,
                JACKSON_DATATYPE_JODA_LIB,
                JAVAX_INJECT_API_LIB,
                JONGO_LIB,
                MONGODB_DRIVER_LIB,
                REFLECTIONS_LIB,
                SOLR_SEARCH_LIB

        integrationTestImplementation SLF4J_LIB,
                LOGBACK_CORE_LIB,
                LOGBACK_CLASSIC_LIB

        implementation (HIBERNATE_LIB) {
            exclude group: 'javax.transaction'
        }
    }

    tasks.register('integrationTest', Test) {
        testClassesDirs += sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

}
