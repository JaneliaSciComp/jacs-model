plugins {
    id "com.github.hierynomus.license" version "0.16.1"
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'
    group = 'org.janelia.jacs-model'
    version = '2.69.0-SNAPSHOT'
}

subprojects {
    apply from: "${rootDir}/deps.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        provided
        implementation.extendsFrom(provided)
        integrationTestCompileOnly.extendsFrom(testCompileOnly)
        integrationTestImplementation.extendsFrom(testImplementation)
        integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    }

    afterEvaluate {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "https://nexus.janelia.org/repository/maven-releases"
            }
            maven {
                url "https://nexus.janelia.org/repository/maven-snapshots"
            }
        }

        sourceSets {
            integrationTest {
                java {
                    compileClasspath += main.output + test.output
                    runtimeClasspath += main.output + test.output
                    srcDir file('src/integrationTest/java')
                }
                resources {
                    srcDir file('src/integrationTest/resources')
                }
            }
        }

        compileJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileIntegrationTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        idea {
            module {
                downloadSources = true
                downloadJavadoc = true
            }
        }

        task integrationTest(type: Test) {
            testClassesDirs += sourceSets.integrationTest.output.classesDirs
            classpath = sourceSets.integrationTest.runtimeClasspath
        }

    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    publishing {
        publications {
            mavenJar(MavenPublication) {
                from components.java
                artifact sourceJar
            }
        }
        ext {
           if (!project.hasProperty('mavenRepoUser')) {
              mavenRepoUser = ''
           }
           if (!project.hasProperty('mavenRepoPassword')) {
              mavenRepoPassword = ''
           }
        }
        repositories {
            maven {
                def releasesRepoUrl = 'https://nexus.janelia.org/repository/maven-releases'
                def snapshotsRepoUrl = 'https://nexus.janelia.org/repository/maven-snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username "${mavenRepoUser}"
                    password "${mavenRepoPassword}"
                }
            }
        }
    }

}

license {
    header rootProject.file('LICENSE.md')
    exclude "**/*"
}

downloadLicenses {
    includeProjectDependencies = true
    dependencyConfiguration = 'compile'
}

task updateWrapper(type: Wrapper) {
    gradleVersion = "7.6"
}

task version {
    println project.version
}
