plugins {
    id 'com.github.hierynomus.license' version '0.16.1'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    group = 'org.janelia.jacs-model'
    version = '4.0.0'
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://nexus.janelia.org/repository/maven-releases"
        }
        maven {
            url "https://nexus.janelia.org/repository/maven-snapshots"
        }
    }

    configurations {
        mockitoAgent { transitive = false }
    }

    def mockitoAgentRef = configurations.getByName("mockitoAgent")

    apply from: "${rootDir}/deps.gradle"


    afterEvaluate {

        compileJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        java {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }

        test {
            useJUnitPlatform()

            jvmArgs("-javaagent:${mockitoAgentRef.asPath}")
        }

        idea {
            module {
                downloadSources = true
                downloadJavadoc = true
            }
        }

    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
        archiveClassifier.set('sources')
    }

    publishing {
        publications {
            mavenJar(MavenPublication) {
                from components.java
                artifact sourceJar
            }
        }
        ext {
           if (!project.hasProperty('mavenRepoUser')) {
              mavenRepoUser = ''
           }
           if (!project.hasProperty('mavenRepoPassword')) {
              mavenRepoPassword = ''
           }
        }
        repositories {
            maven {
                def releasesRepoUrl = 'https://nexus.janelia.org/repository/maven-releases'
                def snapshotsRepoUrl = 'https://nexus.janelia.org/repository/maven-snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username "${mavenRepoUser}"
                    password "${mavenRepoPassword}"
                }
            }
        }
    }

}

license {
    header rootProject.file('LICENSE.md')
    exclude "**/*"
}

downloadLicenses {
    includeProjectDependencies = true
    dependencyConfiguration = 'compile'
}

tasks.register('updateWrapper', Wrapper) {
    gradleVersion = "8.10"
}

tasks.register('version') {
    println project.version
}
