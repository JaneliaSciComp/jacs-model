plugins {
    id "com.github.hierynomus.license" version "0.15.0"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    group = 'org.janelia.jacs-model'
    version = '2.0.1'
}

subprojects {
    apply from: "${rootDir}/deps.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    afterEvaluate {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "https://raw.githubusercontent.com/JaneliaSciComp/mvn-repo/master/repository/"
            }
            flatDir {
                dirs "${rootDir}/projectrepository"
            }
        }

        compileJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    test {
        exclude '**/SampleDAOTest.class'
        exclude '**/SubjectDAOTest.class'
        exclude '**/SampleLockTests.class'
        exclude '**/OntologyMongoDaoTest.class'
        exclude '**/SummaryMongoDaoTest.class'
    }

    publishing {
        publications {
            mavenJar(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier 'sources'
                }
            }
        }
        repositories {
            maven {
                // change to point to your repo, e.g. http://my.org/repo
                url "http://maven.int.janelia.org/nexus/content/repositories/thirdparty"
                credentials {
                    username "deployment"
                    password "deploymentpw"
                }
            }
        }
    }

}

license {
    header rootProject.file('LICENSE.md')
    exclude "**/*"
}

downloadLicenses {
    includeProjectDependencies = true
    dependencyConfiguration = 'compile'
}

task updateWrapper(type: Wrapper) {
    gradleVersion = "5.1.1"
}
